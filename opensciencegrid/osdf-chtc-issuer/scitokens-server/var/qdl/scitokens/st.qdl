/*
   Basic script to fetch the capabilities for a user (by eppn here) and
   put it in the scopes for the access token.
*/
define[
   stAud(x.)
 ][
   aud_caput := 'aud:';
   xi. := mask(x., starts_with(x., aud_caput) == 0); // only those that start with the caput
   return(substring(xi., size(aud_caput)));
 ];

               EPE := 'eduPersonEntitlement';

              cfg. := new_template('file');
   cfg.'file_path' := 'vfs#/scripts/user-config.txt';
   /* Uncomment next two lines if you want to enable default user support */
   cfg.use_default := true;
 cfg.default_claim := 'default_claim';

   // Snarf up the exactly the EPE from the claims using the subject.
              eta. := get_claims(create_source(cfg.), claims.'sub');

access_token.scope := detokenize(unique(eta.EPE), ' ', 2); // turn in to string, omit duplications, trailing space
if[is_defined(eta.'audience')][access_token.'aud' := eta.'audience';];
xi. := stAud(scopes.);
if[0<size(xi.)][access_token.'aud' := xi.;];
if[
   is_defined(claims.'eppn')
][
   access_token.'sub' := claims.'eppn';
]else[
  if[
     is_defined(claims.'email')
  ][
   access_token.'sub' := claims.'email';
  ];
];
